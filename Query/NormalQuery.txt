In ra thông tin của person "Rose_Somchai_Lock"
select ?s ?p ?o 
where {
	?s a class:Person;
		prs:tên_hiển_thị "Rose_Somchai_Lock";
		?p ?o . 
	filter (strstarts(str(?p), "http://btl/properties/" ))
}
***   
In ra thông tin các sự kiện "International_Conference_on_Petroleum_and_Petrochemical_Engineering_(ICPPE)" sẽ được tổ chức tại "India"
select ?s ?p ?o
where {
  	?idx a class:Country;
		prs:tên_hiển_thị "India".
	?s a class:Event;
		prs:tên_hiển_thị "International_Conference_on_Petroleum_and_Petrochemical_Engineering_(ICPPE)";
		rel:sẽ_được_tổ_chức_tại ?idx;
		?p ?o .
	filter (strstarts(str(?p), "http://btl/properties/"))
}
***
In thông tin 10 người đầu tiên tìm thấy sống ở Location "Okayama,_Japan"
select ?s ?p ?o 
where {
  { 
    select ?s where {
      ?idx a class:Location;
           prs:tên_hiển_thị "Okayama,_Japan".
      ?s a class:Person;
           rel:sống_ở ?idx.
	} 
	limit 10
  }
  ?s ?p ?o.
  filter (strstarts(str(?p), "http://btl/properties/" ))
} 
***
In ra số nơi mà công ty "Nintendo" phá sản
select (count(?o) as ?s) 
where {
  ?idx a class:Location;
       prs:tên_hiển_thị ?o . 
  ?idy a class:Organization;
       prs:tên_hiển_thị "Nintendo";
       rel:phá_sản_tại ?idx . 
} 
***
In ra số người sống ở Location "Toronto,_Canada"
select (count(?idy) as ?s)
where {
  ?idx a class:Location;
       prs:tên_hiển_thị "Toronto,_Canada" . 
  ?idy a class:Person;
       rel:sống_ở ?idx . 
}
***
In ra thông tin những nước có dân số thấp hơn 100000 nhưng có GPD cao hơn 5000 
select ?s ?p ?o 
where {
  ?s a class:Country;
       prs:dân_số ?ds;
       prs:GDP ?gdp;
       ?p ?o.
  filter (?ds < 100000 && ?gdp > 5000 && strstarts(str(?p),"http://btl/properties/"))
}
***
In ra những người tên bắt đầu bằng "Rose" và kết thúc là "Black"
select ?s ?p ?o
where {
  ?s a class:Person;
       ?p ?o.
  filter (?p = prs:tên_hiển_thị && regex(?o, "^(Rose)[\\w\\d\\s]*(Black)$"))
}
***
In ra thông tin 5 sự kiện đầu tiên tìm thấy bị hoãn vào 2017-01-31
select ?s ?p ?o 
where {
  {
    select ?s where {	
      ?idx a class:Time;
           prs:tên_hiển_thị "2017-01-31".
      ?s a class:Event;
           rel:bị_hoãn_vào ?idx.	
    }
    limit 5
  }
  ?s ?p ?o.
  filter (strstarts(str(?p), "http://btl/properties"))
} 
***
In ra số lượng các sự kiện được lưu trong csdl
select (count(?idx) as ?s) 
where {
	?idx a class:Event.
}
***
In ra tên hiển thị của những người có số lượng relationship >= 13 cùng với số lượng relationship của người đó
select ?s (count(?_p) as ?p)
where {
  ?o a class:Person;
       ?_p ?id;
       prs:tên_hiển_thị ?s.
  filter (strstarts(str(?_p), "http://btl/relationship/"))
  }
group by ?s 
having (count(?_p) >= 13)
***